plugins {
    id 'idea'
    id 'java'
}

String mainClass = 'com.github.tylerwilliams.tetris.Main'
jar {
    manifest {
        attributes 'Main-Class': mainClass
    }
}

tasks.register('run', JavaExec) {
    main(mainClass)
    classpath sourceSets.main.runtimeClasspath
}

File tetrisRuntimeDir = layout.buildDirectory.dir('runtime').get().asFile

TaskProvider<Exec> jlink = tasks.register('jlink', Exec) {
    group('package')
    dependsOn(classes)

    File mainSources = layout.buildDirectory.dir('classes\\java\\main').get().asFile
    inputs.dir(mainSources)

    outputs.dir(tetrisRuntimeDir)

    // jlink complains if the output dir already exists - this is annoying
    doFirst(tetrisRuntimeDir.&deleteDir)

    commandLine(
        'jlink',
        '--module-path', mainSources.absolutePath,
        '--add-modules', 'tylerwilliams.tetris',
        '--output', tetrisRuntimeDir.absolutePath
    )
}

tasks.register('jpackage', Exec) {
    group('package')
    dependsOn(jlink, jar)

    File inputDir = layout.buildDirectory.dir('libs').get().asFile
    inputs.dir(inputDir)

    File installerDir = layout.buildDirectory.dir('installer').get().asFile
    outputs.dir(installerDir)

    commandLine(
        'jpackage',
        '-n', 'Tetris',
        '--icon', 'src\\main\\resources\\images\\game-icon.ico',
        '--input', inputDir,
        '--runtime-image', tetrisRuntimeDir.absolutePath,
        '--main-jar', jar.archiveFileName.get(),
        '--dest', installerDir
    )
}
